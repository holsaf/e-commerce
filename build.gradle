plugins {
	id 'java'
	id 'scala'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'io.gatling.gradle' version '3.11.5.2'
}

group = 'com.ecommerce'
version = '0.0.1-SNAPSHOT'
description = 'Advanced e-commerce microservice'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
	mavenCentral()
}

dependencies {
    // Scala for Gatling
    implementation 'org.scala-lang:scala-library:2.13.12'
    
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Swagger/OpenAPI - Compatible with Spring Boot 3.3.x
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // Database
    runtimeOnly 'com.h2database:h2'

    // Mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // MySQL Connector
    implementation 'mysql:mysql-connector-java:8.0.33'


    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
	
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/VirtualShopApplication.class',
				'**/config/**',
				'**/entity/**',
				'**/dto/**',
				'**/repository/**',
				'**/exception/**',
				'**/utils/**',
				'**/model/enums/**',
				'**/model/mapper/**Impl.class'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		rule {
			limit {
				minimum = 0.85
			}
		}
	}
	
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/VirtualShopApplication.class',
				'**/config/**',
				'**/entity/**',
				'**/dto/**',
				'**/repository/**',
				'**/exception/**',
				'**/utils/**',
				'**/model/enums/**',
				'**/model/mapper/**Impl.class'
			])
		}))
	}
}

check.dependsOn jacocoTestCoverageVerification

